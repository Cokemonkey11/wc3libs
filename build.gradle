plugins {
    id "java"
    id "antlr"
    id "jacoco"
    id "idea"
}

// Project Info
group 'net.moonlightflower'
version '1.1.0'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    antlr "org.antlr:antlr4:4.7"

    compile 'com.github.inwc3:jmpq3:1.7.1'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'
    compile group: 'com.dorkbox', name: 'PeParser', version: '2.12'
    compile group: 'org.wildfly.swarm', name: 'javafx', version: '2018.5.0'

    testCompile 'org.testng:testng:6.1.1'
}

test {
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

task cleanAntlrDir {
    delete fileTree("src/main/java/net/moonlightflower/wc3libs/antlr")
}

generateGrammarSource { doLast {
    println "Copying generated grammar lexer/parser files to main directory."

    copy {
        from "${buildDir}/generated-src/antlr/main"
        into "src/main/java/net/moonlightflower/wc3libs/antlr"
    }
    project.delete fileTree("${buildDir}/generated-src/antlr/main").include('*.*')
}}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4' // We want gradle 4.4 to run this project
}

generateGrammarSource.dependsOn clean
compileJava.dependsOn generateGrammarSource
