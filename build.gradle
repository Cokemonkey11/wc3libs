plugins {
    id "idea"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.5.0"
}
apply plugin: 'antlr'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

group 'net.moonlightflower'
version '1.0.1'

repositories {
    jcenter()
	mavenCentral()
    maven { url 'https://jitpack.io' }
}

jar.archiveName = "${jar.baseName}.${jar.extension}"
jar {
    from {configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

dependencies {
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.0'

    compile 'com.github.inwc3:JMPQ3:1.5.7'
    compile group: 'com.esotericsoftware.minlog', name: 'minlog', version: '1.2'
    antlr "org.antlr:antlr4:4.5.3"
    testCompile 'org.testng:testng:6.1.1'
}

test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar

    archives sourcesJar
}

generateGrammarSource << {
    println "Copying generated grammar lexer/parser files to main directory."
    copy {
        from "${buildDir}/generated-src/antlr/main"
        into "src/main/java/net/moonlightflower/wc3libs/antlr"
    }
    project.delete fileTree("${buildDir}/generated-src/antlr/main").include('*.*')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10' //we want gradle 2.10 to run this project
}

